#include <SPI.h>
#include <mcp2515.h>
#include <Adafruit_SSD1306.h>
 
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);
 
struct can_frame canMsg;
struct MCP2515 mcp2515(5); // CS pin is GPIO 5
 
#define CAN_ACK_ID 0x037  // CAN ID for acknowledgment
 
void setup()
{
  Serial.begin(115200);
 
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  display.clearDisplay();
  display.setTextColor(WHITE);
 
  SPI.begin();
 
  mcp2515.reset();
  mcp2515.setBitrate(CAN_500KBPS, MCP_8MHZ);
  mcp2515.setNormalMode();
}
 
 
void loop()
{
  if (mcp2515.readMessage(&canMsg) == MCP2515::ERROR_OK)
  {
    if (canMsg.can_id == 0x036)  // Check if the message is from the sender
    {
      int tempInt = (canMsg.data[0] << 8) | canMsg.data[1]; // Combine MSB and LSB
      float temperatureC = tempInt / 100.0; // Convert back to float
 
      Serial.print("Temperature received: ");
      Serial.print(temperatureC);
      Serial.println(" °C");
 
      display.clearDisplay();
 
      display.setTextSize(1);
      display.setCursor(25, 10);
      display.print("Temperature:");
 
      display.setTextSize(2);
      display.setCursor(25, 30);
      display.print(temperatureC);
      display.print("C");
 
      display.display();
 
      // Send acknowledgment
      canMsg.can_id  = CAN_ACK_ID;  // Use ACK ID
      canMsg.can_dlc = 0;           // No data needed for ACK
      mcp2515.sendMessage(&canMsg);
      Serial.println("ACK sent");
    }
  }
}